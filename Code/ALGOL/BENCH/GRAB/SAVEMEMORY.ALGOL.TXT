                                                                        
 $ SET LISTDOLLAR LIST ERRLIST LINEINFO STACK NOXREFLIST                
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%  THIS PROGRAM GRABS SAVE MEMORY - NUMBER OF WORDS DETERMINED BY THE   
%  PARAMETER HOWMUCH. IT THEN WAITS FOR HOWLONG SECONDS BEFORE ENDING.  
%                                                                       
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
PROCEDURE SAVEMEMORY(HOWLONG);                                          
INTEGER HOWLONG;                                                        
BEGIN                                                                   
   DEFINE TYPE = V1.[4:5] #,           % SYSTEMSTATUS defines           
          SUBTYPE = V1.[12:8] #,                                        
          AVAILMEM = SYSTEM_STAT[9] #;                                  
                                                                        
   DIRECT ARRAY MEMORY[1:1000,1:1000]; % Array to grab SAVE memory      
   ARRAY SYSTEM_STAT[0:100];           % SYSTEMSTATUS info array        
   REAL V1,V2;                         % SYSTEMSTATUS variables         
   INTEGER CURRENTROW;                 % Array index                    
   STRING OUT_STRING;                  % String for accept (If failure) 
   INTERRUPT HI;                       % Display message on interrupt   
     DISPLAY("====> HAVE USED "!!STRING(((CURRENTROW-1)*                
             (AVAILMEM MOD 1000)),*)!!" WORDS");                        
                                                                        
   ATTACH HI TO MYSELF.EXCEPTIONEVENT; % Attach interrupt to event      
   TYPE:=2;                            % Set SYSTEMSTATUS variables     
   SUBTYPE:=0;                         % to obtain available memory     
   IF SYSTEMSTATUS(SYSTEM_STAT,V1,V2) THEN                              
   BEGIN                               % If SYSTEMSTATUS failure abort  
      OUT_STRING:="SYSTEMSTATUS FAILURE. ABORTING SAVE MEMORY TEST";    
      ACCEPT(OUT_STRING);                                               
      MYSELF.STATUS:=-1;                                                
   END;                                                                 
                                       % Resize array to use all the    
                                       % available memory (Almost)      
   RESIZE(MEMORY[1,*],(AVAILMEM MOD 1000),DISCARD);                     
                                       % Touch each row to grab memory  
   FOR CURRENTROW := 1 STEP 1 UNTIL 1000 DO                             
     REPLACE MEMORY[CURRENTROW,1] BY "BENCH " FOR                       
                                              (AVAILMEM MOD 1000) WORDS;
   DISPLAY("====> SAVEMEMORY FINISHED");                                
   WAIT((HOWLONG));                    % Wait, then cause interrupt     
   CAUSEANDRESET(MYSELF.EXCEPTIONEVENT);                                
END.                                                                    
