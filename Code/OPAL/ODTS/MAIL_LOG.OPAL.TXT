DEFINE * ODTSEQUENCE MAIL_LOG:                                                  
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%        
%                                                                      %        
% SEQUENCE THAT DETAILS THE MAIL LOGS FOR THE LAST 6 HOURS.            %        
%                                                                      %        
% VERSION 01 CODED BY ANON, IMPLEMENTED BY  - 08/02/2017  %        
%                                                                      %        
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%        
   $Rslt:=TT("MAIL LOG BACK 6 HOURS +");                                        
   $EMAIL:="##REDACTED##";                                      
   $Tmp:=$Rslt;                                                                 
   $Msg:=#("     ---- ",HOSTNAME," Mail Transmission Log ----",/,               
      "     ---- ",DatetoText(Today,DDMMYYYY)," ----",/);                       
   While $Line:=$Tmp.Split(/) Neq Empty Do                                      
   Begin                                                                        
     $Entry:=$Line;                                                             
     $Line:=Drop($Line,9);                                                      
     $Act:=$Line.Split(":");                                                    
     If Length($Trans:=Head($Line.Split(":"),"0123456789"))=5 Then              
     Begin                                                                      
        #Inx:=IndexOf($Act =                                                    
                    {"Rcv","Snd","Att","Inc",                                   
                     "Err","Que"});                                             
        Case #Inx Of                                                            
        Begin                                                                   
        0: #Mail:=#Mail+1;                                                      
        1: If "ReSent OK" IsIn $Line Then                                       
              #Resent:=#ReSent+1;                                               
           #Sent:=#Sent+1;                                                      
        2: #Attached:=#Attached+1;                                              
        3: #Included:=#Included+1;                                              
        4: #Rejected:=#Rejected+1;                                              
           $M_Reject:=&#($Entry,/);                                             
        5: #Queued:=#Queued+1;                                                  
           $M_Reject:=&#($Entry,/);                                             
     Else: ;                                                                    
        End;                                                                    
     End;                                                                       
   End;                                                                         
   $Msg:=&#(#Mail 4," mail requests processed",/,                               
                     #Sent 4," mail(s) Sent",/                                  
                     #Queued 4," mail(s) Queued",/,                             
                     #ReSent 4," mail(s) Re-sent ok",/                          
                     If #Tmp:=#Queued-#ReSent Gtr 0 Then                        
                        #(#Tmp 4," queued mail(s) outstanding",/)               
                     Else                                                       
                        Empty,                                                  
                     #Attached 4," file attachment(s) processed",/,             
                     #Included 4," file include(s) processed",/,                
                     " ",/,                                                     
                     #Rejected 4," mail(s) rejected or aborted",/);             
      If #Rejected Gtr 0 Then                                                   
         $Msg:=&#(/,"     ---- Rejected emails ----",/,                         
               $M_Reject);                                                      
      $Msg:=&#(/,"     ---- Mail listing ---- ",/,$Rslt,/);                     
      If #Mres:=Mail(#("To:",$EMAIL,";Subject:",HOSTNAME,                       
                         " Daily Mail Log ",                                    
                         DatetoText(Today,DDMMYYYY),,                           
                         If #Queued+#Rejected = 0 Then "OK" ELse "Check")       
                         ,$Msg) Lss 0 Then                                      
         If #Mres Eql -999 Then                                                 
            Display("Warning! Mail server unavailable:Report queued")           
         Else                                                                   
            Display("Error #",#Mres," emailing report:",                        
                    MAILERROR(#Mres));                                          
                                                                                
